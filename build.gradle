buildscript {

    ext {
        kotlinVersion = '1.9.22'
        springBootVersion = '3.1.5'
        springfoxVersion = '3.0.0'
        jacksonModuleKotlinVersion = '2.16.0'
        tokenSupportVersion = '3.2.0'
        springKafkaVersion = '3.0.12'
    }
}

plugins {
    id "org.springframework.boot" version "$springBootVersion"
    id "io.spring.dependency-management" version "1.1.4"
    id "org.openrepose.gradle.plugins.jaxb" version "2.5.0"
    id "com.vanniktech.dependency.graph.generator" version "0.8.0"
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "$kotlinVersion"
}

apply from: "buildSrc/ep-module.gradle"

group = 'no.nav.eessi.pensjon'

jar {
    archivesBaseName = 'eessi-fagmodul'
}

dependencies {

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-cache")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-actuator")

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:6.1.1"))

    implementation("org.yaml:snakeyaml:2.2") // Pga https://security.snyk.io/package/maven/org.yaml:snakeyaml

    // Kotlin
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonModuleKotlinVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonModuleKotlinVersion")

    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:7.4")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'

    // auditlogger syslog la stå i 1.0.0 / 0.9.30
    implementation("com.papertrailapp:logback-syslog4j:1.0.0")
    implementation("org.syslog4j:syslog4j:0.9.46")

    //auth
    implementation("no.nav.security:token-client-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${tokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${tokenSupportVersion}")

    implementation ("no.nav.common:token-client:3.2023.09.13_04.55-a8ff452fbd94")
    implementation("org.springframework.kafka:spring-kafka:${springKafkaVersion}")

    //GCP
    implementation("com.google.cloud:google-cloud-storage:2.30.1")

    implementation("no.nav.eessi.pensjon:ep-metrics:2.0.46")
    implementation("no.nav.eessi.pensjon:ep-logging:2.1.41")
    implementation("no.nav.eessi.pensjon:ep-eux:2.1.88")
    implementation("no.nav.eessi.pensjon:ep-kodeverk:4.0.100") {
        exclude module: 'ep-eux'
        exclude module: 'ep-logging'
        exclude module: 'ep-metrics'
    }

    implementation("no.nav.eessi.pensjon:ep-personoppslag:13.1.54") {
        exclude module: 'ep-eux'
        exclude module: 'ep-logging'
        exclude module: 'ep-metrics'
    }

    implementation("no.nav.eessi.pensjon:ep-pensjonsinformasjon:3.0.79") {
        exclude module: 'ep-eux'
        exclude module: 'ep-logging'
        exclude module: 'ep-metrics'
    }

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation('org.glassfish.jaxb:jaxb-runtime:2.3.3')
    implementation("no.nav.tjenestespesifikasjoner:pensjonsinformasjon-v1-tjenestespesifikasjon:2618.0448179")

    testImplementation("no.nav.security:token-validation-spring-test:${tokenSupportVersion}")

    // Forenklere mocking av beans, benyttes som mockito mockbean
    testImplementation("com.ninja-squad:springmockk:4.0.2")
    testImplementation('org.pac4j:spring-webmvc-pac4j:8.0.0')

    testImplementation(platform("org.junit:junit-bom:5.10.1"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    //mock - test
    testImplementation('io.mockk:mockk:1.13.8')
    testImplementation("org.skyscreamer:jsonassert:1.5.1")

    //Kafka
    testImplementation("org.springframework.kafka:spring-kafka-test:${springKafkaVersion}")

    testImplementation("com.tngtech.archunit:archunit:1.2.1")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: 'junit'
        exclude module: "mockito-core"
        exclude module: 'junit-vintage-engine'
    }
}

repositories {
    maven {
        url 'https://repo.clojars.org'
        name 'Clojars'
    }
}
