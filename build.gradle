buildscript {
    ext {
        kotlinVersion = '1.2.71'
        springBootVersion = '2.0.4.RELEASE'
        springSecurityVersion = '5.0.5.RELEASE'
        prometheusVersion = '0.5.0'
        springfoxVersion = '2.9.2'
        jacksonModuleKotlinVersion = '2.9.6'
        cxfVersion = '3.2.6'
        mockitoVersion = '2.8.+'
        personV3Version = '3.4.1'
        oidcTokenSupportVersion = '0.2.7'
        logstashLogbackVersion = '4.10'
    }
    repositories {
        maven {
            url "https://repo.adeo.no/repository/maven-central"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("gradle.plugin.org.openrepose:gradle-jaxb-plugin:2.5.0")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.openrepose.gradle.plugins.jaxb'

group = 'no.nav.eessi'
version = '0.0.1'
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
jar {
    baseName = 'eessi-fagmodul'
}

configurations {
    xsd
}

dependencies {

    // Prometheus
    compile("io.prometheus:simpleclient:${prometheusVersion}")
    compile("io.prometheus:simpleclient_hotspot:${prometheusVersion}")
    compile("io.prometheus:simpleclient_httpserver:${prometheusVersion}")
    compile("io.prometheus:simpleclient_pushgateway:${prometheusVersion}")

    // Spring-boot
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    testCompile("org.springframework.boot:spring-boot-starter-test")

    // Apache CXF
    compile("org.apache.cxf:cxf-spring-boot-starter-jaxws:${cxfVersion}")
    compile("org.apache.cxf:cxf-rt-ws-security:${cxfVersion}")

    // Kotlin
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonModuleKotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

    // Swagger
    compile("io.springfox:springfox-swagger2:${springfoxVersion}")
    compile("io.springfox:springfox-swagger-ui:${springfoxVersion}")

    //mock - test
    testCompile("org.mockito:mockito-core:${mockitoVersion}")
    testCompile("org.jetbrains.kotlin:kotlin-test")
    testCompile("org.jetbrains.kotlin:kotlin-test-junit")
    testCompile("com.nhaarman:mockito-kotlin-kt1.1:1.5.0")
    testCompile("org.skyscreamer:jsonassert:1.5.0")

    // Logging
    compile("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")

    // OIDC
    compile("no.nav.security:oidc-spring-support:${oidcTokenSupportVersion}")

    // Tjenestespesifikasjoner
    compile("no.nav.tjenester:nav-person-v3-tjenestespesifikasjon:${personV3Version}")
    xsd("no.nav.pensjon:pensjonsinformasjon-xsd:9.3.9")
    compile("no.nav.pensjon:pensjonsinformasjon-xsd:9.3.9")

    jaxb 'org.glassfish.jaxb:jaxb-xjc:2.2.11'
    jaxb 'org.glassfish.jaxb:jaxb-runtime:2.2.11'
}

ext.schemasDir = "${projectDir}/src/main/resources/schemas"
ext.pensjonsinformasjonDir = "${schemasDir}/${configurations.xsd.dependencies[0].name}_${configurations.xsd.dependencies[0].version}"

task copyPensjonsinformasjonXsdsFromArtifacts(type: Copy) {
    includeEmptyDirs = false
    from {
        configurations.xsd.asFileTree.forEach { artifact ->
            from zipTree(artifact)
        }
    }
    into "${pensjonsinformasjonDir}"
    include '**/validation/*.xsd'
}

jaxb {
    xsdDir = schemasDir
}

xjc.dependsOn copyPensjonsinformasjonXsdsFromArtifacts

compileKotlin.dependsOn xjc

clean.doFirst {
    delete pensjonsinformasjonDir
}

sourceSets["main"].java.srcDir("${buildDir}/generated-sources/xjc")

repositories {
    maven {
        url "https://repo.adeo.no/repository/maven-central"
    }
    maven {
        url "https://repo.adeo.no/repository/maven-releases"
    }
}
